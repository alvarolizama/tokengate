defmodule TokengateWeb.CoreComponents do
  @moduledoc """
  Provides core UI components.

  At first glance, this module may seem daunting, but its goal is to provide
  core building blocks for your application, such as tables, forms, and
  inputs. The components consist mostly of markup and are well-documented
  with doc strings and declarative assigns. You may customize and style
  them in any way you want, based on your application growth and needs.

  The foundation for styling is Tailwind CSS, a utility-first CSS framework,
  augmented with daisyUI, a Tailwind CSS plugin that provides UI components
  and themes. Here are useful references:

    * [daisyUI](https://daisyui.com/docs/intro/) - a good place to get
      started and see the available components.

    * [Tailwind CSS](https://tailwindcss.com) - the foundational framework
      we build on. You will use it for layout, sizing, flexbox, grid, and
      spacing.

    * [Heroicons](https://heroicons.com) - see `icon/1` for usage.

    * [Phoenix.Component](https://phoenix-live-view.hexdocs.pm/Phoenix.Component.html) -
      the component system used by Phoenix. Some components, such as `<.link>`
      and `<.form>`, are defined there.

  """
  use Phoenix.Component
  use Gettext, backend: TokengateWeb.Gettext
  alias Phoenix.LiveView.JS

  @doc """
  Renders flash notices.

  ## Examples

      <.flash kind={:info} flash={@flash} />
      <.flash
        id="welcome-back"
        kind={:info}
        phx-mounted={show("#welcome-back") |> JS.remove_attribute("hidden")}
        hidden
      >
        Welcome Back!
      </.flash>
  """
  attr :id, :string, doc: "the optional id of flash container"
  attr :flash, :map, default: %{}, doc: "the map of flash messages to display"
  attr :title, :string, default: nil
  attr :kind, :atom, values: [:info, :error], doc: "used for styling and flash lookup"
  attr :rest, :global, doc: "the arbitrary HTML attributes to add to the flash container"

  slot :inner_block, doc: "the optional inner block that renders the flash message"

  def flash(assigns) do
    assigns = assign_new(assigns, :id, fn -> "flash-#{assigns.kind}" end)

    ~H"""
    <div
      :if={msg = render_slot(@inner_block) || Phoenix.Flash.get(@flash, @kind)}
      id={@id}
      phx-click={JS.push("lv:clear-flash", value: %{key: @kind}) |> hide("##{@id}")}
      role="alert"
      class="toast toast-top toast-end z-50"
      {@rest}
    >
      <div class={[
        "alert w-80 sm:w-96 max-w-80 sm:max-w-96 text-wrap",
        @kind == :info && "alert-info",
        @kind == :error && "alert-error"
      ]}>
        <.icon :if={@kind == :info} name="hero-information-circle" class="size-5 shrink-0" />
        <.icon :if={@kind == :error} name="hero-exclamation-circle" class="size-5 shrink-0" />
        <div>
          <p :if={@title} class="font-semibold">{@title}</p>
          <p>{msg}</p>
        </div>
        <div class="flex-1" />
        <button type="button" class="group self-start cursor-pointer" aria-label={gettext("close")}>
          <.icon name="hero-x-mark" class="size-5 opacity-40 group-hover:opacity-70" />
        </button>
      </div>
    </div>
    """
  end

  @doc """
  Renders a button with navigation support.

  ## Examples

      <.button>Send!</.button>
      <.button phx-click="go" variant="primary">Send!</.button>
      <.button navigate={~p"/"}>Home</.button>
  """
  attr :rest, :global, include: ~w(href navigate patch method download name value disabled)
  attr :class, :any
  attr :variant, :string, values: ~w(primary)
  slot :inner_block, required: true

  def button(%{rest: rest} = assigns) do
    variants = %{"primary" => "btn-primary", nil => "btn-primary btn-soft"}

    assigns =
      assign_new(assigns, :class, fn ->
        ["btn", Map.fetch!(variants, assigns[:variant])]
      end)

    if rest[:href] || rest[:navigate] || rest[:patch] do
      ~H"""
      <.link class={@class} {@rest}>
        {render_slot(@inner_block)}
      </.link>
      """
    else
      ~H"""
      <button class={@class} {@rest}>
        {render_slot(@inner_block)}
      </button>
      """
    end
  end

  @doc """
  Renders an input with label and error messages.

  A `Phoenix.HTML.FormField` may be passed as argument,
  which is used to retrieve the input name, id, and values.
  Otherwise all attributes may be passed explicitly.

  ## Types

  This function accepts all HTML input types, considering that:

    * You may also set `type="select"` to render a `<select>` tag

    * `type="checkbox"` is used exclusively to render boolean values

    * For live file uploads, see `Phoenix.Component.live_file_input/1`

  See https://developer.mozilla.org/en-US/docs/Web/HTML/Element/input
  for more information. Unsupported types, such as radio, are best
  written directly in your templates.

  ## Examples

  ```heex
  <.input field={@form[:email]} type="email" />
  <.input name="my-input" errors={["oh no!"]} />
  ```

  ## Select type

  When using `type="select"`, you must pass the `options` and optionally
  a `value` to mark which option should be preselected.

  ```heex
  <.input field={@form[:user_type]} type="select" options={["Admin": "admin", "User": "user"]} />
  ```

  For more information on what kind of data can be passed to `options` see
  [`options_for_select`](https://phoenix-html.hexdocs.pm/Phoenix.HTML.Form.html#options_for_select/2).
  """
  attr :id, :any, default: nil
  attr :name, :any
  attr :label, :string, default: nil
  attr :value, :any

  attr :type, :string,
    default: "text",
    values: ~w(checkbox color date datetime-local email file month number password
               search select tel text textarea time url week hidden datalist)

  attr :field, Phoenix.HTML.FormField,
    doc: "a form field struct retrieved from the form, for example: @form[:email]"

  attr :errors, :list, default: []
  attr :checked, :boolean, doc: "the checked flag for checkbox inputs"
  attr :prompt, :string, default: nil, doc: "the prompt for select inputs"
  attr :options, :list, doc: "the options to pass to Phoenix.HTML.Form.options_for_select/2"
  attr :multiple, :boolean, default: false, doc: "the multiple flag for select inputs"
  attr :class, :any, default: nil, doc: "the input class to use over defaults"
  attr :error_class, :any, default: nil, doc: "the input error class to use over defaults"
  attr :hint, :string, default: nil, doc: "help text shown below the input"
  attr :placeholder, :string, default: nil, doc: "placeholder text for text-like inputs"

  attr :rest, :global,
    include: ~w(accept autocomplete capture cols disabled form list max maxlength min minlength
                multiple pattern placeholder readonly required rows size step)

  def input(%{field: %Phoenix.HTML.FormField{} = field} = assigns) do
    errors = if Phoenix.Component.used_input?(field), do: field.errors, else: []

    assigns
    |> assign(field: nil, id: assigns.id || field.id)
    |> assign(:errors, Enum.map(errors, &translate_error(&1)))
    |> assign_new(:name, fn -> if assigns.multiple, do: field.name <> "[]", else: field.name end)
    |> assign_new(:value, fn -> field.value end)
    |> input()
  end

  def input(%{type: "hidden"} = assigns) do
    ~H"""
    <input type="hidden" id={@id} name={@name} value={@value} {@rest} />
    """
  end

  def input(%{type: "checkbox"} = assigns) do
    assigns =
      assign_new(assigns, :checked, fn ->
        Phoenix.HTML.Form.normalize_value("checkbox", assigns[:value])
      end)

    ~H"""
    <div class="fieldset mb-2">
      <label for={@id}>
        <input
          type="hidden"
          name={@name}
          value="false"
          disabled={@rest[:disabled]}
          form={@rest[:form]}
        />
        <span class="label">
          <input
            type="checkbox"
            id={@id}
            name={@name}
            value="true"
            checked={@checked}
            class={@class || "checkbox checkbox-sm"}
            {@rest}
          />{@label}
        </span>
      </label>
      <.error :for={msg <- @errors}>{msg}</.error>
      <p :if={@hint} class="text-xs text-base-content/50 mt-1">{@hint}</p>
    </div>
    """
  end

  def input(%{type: "select"} = assigns) do
    ~H"""
    <div class="fieldset mb-2">
      <label for={@id}>
        <span :if={@label} class="label mb-1">{@label}</span>
        <select
          id={@id}
          name={@name}
          class={[@class || "w-full select", @errors != [] && (@error_class || "select-error")]}
          multiple={@multiple}
          {@rest}
        >
          <option :if={@prompt} value="">{@prompt}</option>
          {Phoenix.HTML.Form.options_for_select(@options, @value)}
        </select>
      </label>
      <.error :for={msg <- @errors}>{msg}</.error>
      <p :if={@hint} class="text-xs text-base-content/50 mt-1">{@hint}</p>
    </div>
    """
  end

  # A free-text input with autocomplete suggestions (HTML5 <datalist>).
  # Lets users pick from a known set of options OR type any custom value —
  # useful for upstream model strings that aren't in the public catalog
  # (e.g. dedicated tiers, private deployments, namespaced variants).
  def input(%{type: "datalist"} = assigns) do
    ~H"""
    <div class="fieldset mb-2">
      <label for={@id}>
        <span :if={@label} class="label mb-1">{@label}</span>
        <input
          type="text"
          id={@id}
          name={@name}
          value={Phoenix.HTML.Form.normalize_value("text", @value)}
          list={@id <> "-list"}
          class={[
            @class || "w-full input",
            @errors != [] && (@error_class || "input-error")
          ]}
          placeholder={@placeholder}
          {@rest}
        />
        <datalist id={@id <> "-list"}>
          <option :for={opt <- @options} value={opt} />
        </datalist>
      </label>
      <.error :for={msg <- @errors}>{msg}</.error>
      <p :if={@hint} class="text-xs text-base-content/50 mt-1">{@hint}</p>
    </div>
    """
  end

  def input(%{type: "textarea"} = assigns) do
    ~H"""
    <div class="fieldset mb-2">
      <label for={@id}>
        <span :if={@label} class="label mb-1">{@label}</span>
        <textarea
          id={@id}
          name={@name}
          class={[
            @class || "w-full textarea",
            @errors != [] && (@error_class || "textarea-error")
          ]}
          placeholder={@placeholder}
          {@rest}
        >{Phoenix.HTML.Form.normalize_value("textarea", @value)}</textarea>
      </label>
      <.error :for={msg <- @errors}>{msg}</.error>
      <p :if={@hint} class="text-xs text-base-content/50 mt-1">{@hint}</p>
    </div>
    """
  end

  # All other inputs text, datetime-local, url, password, etc. are handled here...
  def input(assigns) do
    ~H"""
    <div class="fieldset mb-2">
      <label for={@id}>
        <span :if={@label} class="label mb-1">{@label}</span>
        <input
          type={@type}
          name={@name}
          id={@id}
          value={Phoenix.HTML.Form.normalize_value(@type, @value)}
          class={[
            @class || "w-full input",
            @errors != [] && (@error_class || "input-error")
          ]}
          placeholder={@placeholder}
          {@rest}
        />
      </label>
      <.error :for={msg <- @errors}>{msg}</.error>
      <p :if={@hint} class="text-xs text-base-content/50 mt-1">{@hint}</p>
    </div>
    """
  end

  # Helper used by inputs to generate form errors
  defp error(assigns) do
    ~H"""
    <p class="mt-1.5 flex gap-2 items-center text-sm text-error">
      <.icon name="hero-exclamation-circle" class="size-5" />
      {render_slot(@inner_block)}
    </p>
    """
  end

  @doc """
  Renders a header with title.
  """
  slot :inner_block, required: true
  slot :subtitle
  slot :actions

  def header(assigns) do
    ~H"""
    <header class={[@actions != [] && "flex items-center justify-between gap-6", "pb-4"]}>
      <div>
        <h1 class="text-lg font-semibold leading-8">
          {render_slot(@inner_block)}
        </h1>
        <p :if={@subtitle != []} class="text-sm text-base-content/70">
          {render_slot(@subtitle)}
        </p>
      </div>
      <div class="flex-none">{render_slot(@actions)}</div>
    </header>
    """
  end

  @doc """
  Renders a table with generic styling.

  ## Examples

      <.table id="users" rows={@users}>
        <:col :let={user} label="id">{user.id}</:col>
        <:col :let={user} label="username">{user.username}</:col>
      </.table>
  """
  attr :id, :string, required: true
  attr :rows, :list, required: true
  attr :row_id, :any, default: nil, doc: "the function for generating the row id"
  attr :row_click, :any, default: nil, doc: "the function for handling phx-click on each row"

  attr :row_item, :any,
    default: &Function.identity/1,
    doc: "the function for mapping each row before calling the :col and :action slots"

  slot :col, required: true do
    attr :label, :string
  end

  slot :action, doc: "the slot for showing user actions in the last table column"

  def table(assigns) do
    assigns =
      with %{rows: %Phoenix.LiveView.LiveStream{}} <- assigns do
        assign(assigns, row_id: assigns.row_id || fn {id, _item} -> id end)
      end

    ~H"""
    <table class="table table-sm">
      <thead>
        <tr>
          <th :for={col <- @col}>{col[:label]}</th>
          <th :if={@action != []}>
            <span class="sr-only">{gettext("Actions")}</span>
          </th>
        </tr>
      </thead>
      <tbody id={@id} phx-update={is_struct(@rows, Phoenix.LiveView.LiveStream) && "stream"}>
        <tr :for={row <- @rows} id={@row_id && @row_id.(row)}>
          <td
            :for={col <- @col}
            phx-click={@row_click && @row_click.(row)}
            class={@row_click && "hover:cursor-pointer"}
          >
            {render_slot(col, @row_item.(row))}
          </td>
          <td :if={@action != []} class="w-0 font-semibold">
            <div class="flex gap-4">
              <%= for action <- @action do %>
                {render_slot(action, @row_item.(row))}
              <% end %>
            </div>
          </td>
        </tr>
      </tbody>
    </table>
    """
  end

  @doc """
  Renders a data list.

  ## Examples

      <.list>
        <:item title="Title">{@post.title}</:item>
        <:item title="Views">{@post.views}</:item>
      </.list>
  """
  slot :item, required: true do
    attr :title, :string, required: true
  end

  def list(assigns) do
    ~H"""
    <ul class="list">
      <li :for={item <- @item} class="list-row">
        <div class="list-col-grow">
          <div class="font-bold">{item.title}</div>
          <div>{render_slot(item)}</div>
        </div>
      </li>
    </ul>
    """
  end

  @doc """
  Renders a [Heroicon](https://heroicons.com).

  Heroicons come in three styles – outline, solid, and mini.
  By default, the outline style is used, but solid and mini may
  be applied by using the `-solid` and `-mini` suffix.

  You can customize the size and colors of the icons by setting
  width, height, and background color classes.

  Icons are extracted from the `deps/heroicons` directory and bundled within
  your compiled app.css by the plugin in `assets/vendor/heroicons.js`.

  ## Examples

      <.icon name="hero-x-mark" />
      <.icon name="hero-arrow-path" class="ml-1 size-3 motion-safe:animate-spin" />
  """
  attr :name, :string, required: true
  attr :class, :any, default: "size-4"

  def icon(%{name: "hero-" <> _} = assigns) do
    ~H"""
    <span class={[@name, @class]} />
    """
  end

  @doc """
  Renders a shared model picker: toggleable badges for model grants.

  Shared by Groups, Services and Group Members. Each badge fires the given
  `toggle_event` with the model id plus the target id (group/service/member),
  passed via `phx-value-target-key`/`phx-value-target-value`.

  ## Examples

      <.model_picker
        id="model-picker-group"
        models={@models}
        granted_ids={@granted_ids}
        toggle_event="toggle_model"
        target_key="group-id"
        target_value={group.id}
      />
  """
  attr :id, :string, required: true
  attr :models, :list, required: true
  attr :granted_ids, :list, required: true
  attr :toggle_event, :string, required: true
  attr :target_value, :string, required: true
  attr :locked_ids, :list, default: []
  attr :extra_ids, :list, default: []
  attr :denied_ids, :list, default: []
  attr :empty_text, :string, default: "No hay modelos disponibles."

  def model_picker(assigns) do
    ~H"""
    <div class="flex flex-wrap gap-2" id={@id}>
      <button
        :for={model <- @models}
        type="button"
        phx-click={@toggle_event}
        phx-value-target-id={@target_value}
        phx-value-model-id={model.id}
        class={[
          "badge badge-sm transition-all",
          cond do
            model.id in @locked_ids -> "badge-primary opacity-60"
            model.id in @denied_ids -> "badge-error badge-outline line-through opacity-70"
            model.id in @extra_ids -> "badge-accent"
            model.id in @granted_ids -> "badge-primary"
            true -> "badge-outline cursor-pointer hover:badge-primary/50"
          end
        ]}
        id={"#{@id}-#{model.id}"}
        disabled={model.id in @locked_ids}
        title={
          cond do
            model.id in @locked_ids -> gettext("Granted by your limit profile")
            model.id in @denied_ids -> gettext("Removed for this member (click to restore)")
            model.id in @extra_ids -> "Extra individual"
            true -> nil
          end
        }
      >
        {model.name}
      </button>
      <p :if={@models == []} class="text-xs text-base-content/40">
        {@empty_text}
      </p>
    </div>
    """
  end

  ## ── Navegación y superficies compartidas ─────────────────────────────────
  #
  # Primitivas que comparten el shell (sidebar, rail y menú de usuario) con las
  # páginas. Viven acá —no en Layouts— para que cualquier LiveView las tenga por
  # el `use TokengateWeb, :html`, sin imports (Commons C4).

  @doc """
  Enlace de navegación para sidebars y rails: icono + label + badge opcional.

  Estados: activo = `bg-primary/15 text-primary font-medium` + `aria-current`;
  libre = `btn-ghost`. **No** se usa `btn-primary btn-soft` para el activo:
  mezcla sólo 8% del color con `base-100` y el pill se lee gris (Commons C12.3).

  El `title` lleva el label: es el tooltip del rail colapsado. El `id` se deriva
  del path (`sidebar-link-catalog-models`) salvo que se pase uno explícito — es
  la convención de tests de TokenGate.

  ## Ejemplo

      <.nav_link label={gettext("Models")} icon="hero-rectangle-stack" path={~p"/catalog/models"} active={@active} />
  """
  attr :label, :string, required: true
  attr :icon, :string, required: true, doc: ~s(hero icon name, e.g. "hero-home")
  attr :path, :any, required: true, doc: "href of the destination"
  attr :active, :boolean, default: false, doc: "marks the current page (aria-current)"
  attr :badge, :any, default: nil, doc: "optional count; renders only when > 0"
  attr :badge_kind, :string, default: nil, doc: ~s(semantic badge class, e.g. "badge-error")
  attr :id, :string, default: nil, doc: "DOM id; derived from the path when omitted"

  def nav_link(assigns) do
    assigns = assign(assigns, :dom_id, assigns[:id] || "sidebar-link-" <> path_id(assigns.path))

    ~H"""
    <a
      href={@path}
      id={@dom_id}
      title={@label}
      aria-current={@active && "page"}
      class={[
        "nav-link btn btn-sm w-full justify-between gap-2 font-normal transition-colors duration-150 focus-visible:ring-2 focus-visible:ring-primary focus-visible:outline-none",
        @active && "bg-primary/15 text-primary font-medium hover:bg-primary/20",
        !@active && "btn-ghost text-base-content/80 hover:text-base-content"
      ]}
    >
      <span class="flex items-center gap-2 min-w-0">
        <.icon name={@icon} class="size-4 shrink-0" />
        <span class="truncate shell-hide">{@label}</span>
      </span>
      <span
        :if={@badge && @badge > 0}
        class={[
          "badge badge-sm shell-hide",
          @badge_kind || if(@active, do: "badge-primary", else: "badge-ghost")
        ]}
      >
        {@badge}
      </span>
    </a>
    """
  end

  @doc """
  Rótulo de grupo de navegación + sus enlaces.

  ## Ejemplo

      <.nav_group label={gettext("Catalog")} id="sidebar-section-catalogo">
        <.nav_link label={gettext("Labs")} icon="hero-beaker" path={~p"/catalog/labs"} />
      </.nav_group>
  """
  attr :label, :string, required: true
  attr :id, :string, default: nil
  slot :inner_block, required: true

  def nav_group(assigns) do
    ~H"""
    <div class="flex flex-col gap-1" id={@id}>
      <div class="shell-hide px-3 pt-1 pb-1 text-xs font-semibold uppercase tracking-wider text-base-content/70">
        {@label}
      </div>
      {render_slot(@inner_block)}
    </div>
    """
  end

  @doc """
  Entrada de un menú (menú de usuario del shell). Con `href` es un enlace;
  sin él, un botón que dispara `on_click`.

  El activo se marca con `aria-current="page"` + `text-primary`.

  ## Ejemplo

      <.menu_item href={~p"/"} icon="hero-squares-2x2" label={gettext("Workspaces")} />
  """
  attr :label, :string, required: true
  attr :icon, :string, default: nil, doc: "hero icon name"
  attr :href, :string, default: nil
  attr :on_click, :any, default: nil, doc: "phx-click del item cuando no hay href"
  attr :active, :boolean, default: false

  def menu_item(assigns) do
    ~H"""
    <a
      :if={@href}
      href={@href}
      aria-current={@active && "page"}
      class={[
        "flex items-center gap-2 px-3 py-1.5 text-sm hover:bg-base-200 transition-colors",
        @active && "text-primary"
      ]}
    >
      <.icon :if={@icon} name={@icon} class="size-4 opacity-70" />
      {@label}
    </a>
    <button
      :if={!@href}
      type="button"
      phx-click={@on_click}
      aria-current={@active && "page"}
      class={[
        "flex items-center gap-2 px-3 py-1.5 text-sm w-full text-left hover:bg-base-200 transition-colors",
        @active && "text-primary"
      ]}
    >
      <.icon :if={@icon} name={@icon} class="size-4 opacity-70" />
      {@label}
    </button>
    """
  end

  @doc """
  Sección: la caja canónica de TokenGate (Commons C5) con header de badge +
  título + caption opcional. Es la forma de agrupar contenido en una página.

  ## Ejemplo

      <.section title={gettext("Providers")} icon="hero-server-stack" caption={…}>
        <table class="table table-sm">…</table>
      </.section>
  """
  attr :title, :string, required: true
  attr :icon, :string, required: true, doc: "hero icon name del badge del header"
  attr :caption, :string, default: nil
  attr :id, :string, default: nil
  slot :inner_block, required: true

  def section(assigns) do
    ~H"""
    <section class="card bg-base-100 border border-base-300 shadow-sm overflow-hidden" id={@id}>
      <header class="flex items-start gap-3 px-4 py-3 border-b border-base-300">
        <div class="shrink-0 size-8 rounded-lg flex items-center justify-center bg-primary/10">
          <.icon name={@icon} class="size-4 text-primary" />
        </div>
        <div class="min-w-0 flex-1">
          <h2 class="card-title text-base">{@title}</h2>
          <p :if={@caption} class="text-xs text-base-content/50 mt-0.5">{@caption}</p>
        </div>
      </header>
      <div class="p-4">
        {render_slot(@inner_block)}
      </div>
    </section>
    """
  end

  @doc """
  Modal compacto (Commons C7.1): overlay + card, cierre por ✕, Escape y
  click-away. El caller lo gatea con `:if` (no hay estado interno).

  El cuerpo (form + footer de cancelar/guardar) va en el slot por defecto.

  ## Ejemplo

      <.modal :if={@form} id="thing-modal" title={gettext("New thing")} on_close="cancel_form">
        <.form for={@form} id="thing-form" phx-submit="save">…</.form>
      </.modal>
  """
  attr :id, :string, required: true
  attr :title, :string, required: true
  attr :on_close, :string, required: true, doc: "evento LiveView del ✕, Escape y click-away"
  attr :max_w, :string, default: "max-w-lg", doc: "ancho máximo del card (max-w-md|lg|2xl…)"
  slot :inner_block, required: true

  def modal(assigns) do
    ~H"""
    <div
      id={@id}
      class="fixed inset-0 z-50 flex items-center justify-center p-4"
      phx-window-keydown={@on_close}
      phx-key="Escape"
    >
      <div class="absolute inset-0 bg-black/50" phx-click={@on_close} />
      <div class={["relative card bg-base-100 border border-base-300 shadow-xl w-full", @max_w]}>
        <div class="card-body p-6">
          <div class="flex items-center justify-between gap-4 mb-2">
            <h2 class="text-lg font-semibold">{@title}</h2>
            <button
              type="button"
              phx-click={@on_close}
              class="btn btn-ghost btn-xs btn-circle shrink-0"
              aria-label={gettext("Close")}
            >
              <.icon name="hero-x-mark" class="size-4" />
            </button>
          </div>
          {render_slot(@inner_block)}
        </div>
      </div>
    </div>
    """
  end

  @doc """
  Estado vacío canónico (Commons C6/C10): icono + título + caption centrados y
  un slot para el CTA. Va **fuera** de la tabla/colección, nunca dentro.

  ## Ejemplo

      <.empty_state id="labs-empty" icon="hero-beaker" title={gettext("No labs yet")} caption={…} />
  """
  attr :icon, :string, required: true
  attr :title, :string, required: true
  attr :caption, :string, default: nil
  attr :id, :string, default: nil
  attr :class, :string, default: nil, doc: "classes extra del contenedor"
  slot :inner_block, doc: "CTA / acción opcional bajo el caption"

  def empty_state(assigns) do
    ~H"""
    <div class={["flex flex-col items-center justify-center text-center py-12", @class]} id={@id}>
      <.icon name={@icon} class="size-10 mb-2 opacity-40 text-base-content/40" />
      <p class="text-sm font-medium text-base-content/60">{@title}</p>
      <p :if={@caption} class="text-xs text-base-content/40 mt-1 max-w-sm">{@caption}</p>
      <div :if={@inner_block != []} class="mt-4">{render_slot(@inner_block)}</div>
    </div>
    """
  end

  # El id de un enlace de navegación sale del path: `/catalog/models` →
  # `catalog-models`. Es la convención de ids del sidebar (los tests la leen).
  defp path_id(path) do
    path
    |> to_string()
    |> String.trim_leading("/")
    |> String.replace("/", "-")
  end

  ## JS Commands

  def show(js \\ %JS{}, selector) do
    JS.show(js,
      to: selector,
      time: 300,
      transition:
        {"transition-all ease-out duration-300",
         "opacity-0 translate-y-4 sm:translate-y-0 sm:scale-95",
         "opacity-100 translate-y-0 sm:scale-100"}
    )
  end

  def hide(js \\ %JS{}, selector) do
    JS.hide(js,
      to: selector,
      time: 200,
      transition:
        {"transition-all ease-in duration-200", "opacity-100 translate-y-0 sm:scale-100",
         "opacity-0 translate-y-4 sm:translate-y-0 sm:scale-95"}
    )
  end

  @doc """
  Translates an error message using gettext.
  """
  def translate_error({msg, opts}) do
    # When using gettext, we typically pass the strings we want
    # to translate as a static argument:
    #
    #     # Translate the number of files with plural rules
    #     dngettext("errors", "1 file", "%{count} files", count)
    #
    # However the error messages in our forms and APIs are generated
    # dynamically, so we need to translate them by calling Gettext
    # with our gettext backend as first argument. Translations are
    # available in the errors.po file (as we use the "errors" domain).
    if count = opts[:count] do
      Gettext.dngettext(TokengateWeb.Gettext, "errors", msg, msg, count, opts)
    else
      Gettext.dgettext(TokengateWeb.Gettext, "errors", msg, opts)
    end
  end

  @doc """
  Translates the errors for a field from a keyword list of errors.
  """
  def translate_errors(errors, field) when is_list(errors) do
    for {^field, {msg, opts}} <- errors, do: translate_error({msg, opts})
  end
end
